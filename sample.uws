call UwscWebDriver.uws

select slctbox(SLCT_BTN, 0, "操作を選択", "ブラウザ操作を試す", "WebDriverダウンロード")
    case SLCT_1
    case SLCT_2
        v = WebDriverDownload.Dialog()
        if length(v) then
            msgbox("WebDriverバージョン: " + v)
        endif
        exit
    default
        exit
selend

select slctbox(SLCT_BTN or SLCT_STR, 0, "ブラウザを選択", "Chrome", "Firefox", "MsEdge")
    case "Chrome"
        driver = WebDriver.Chrome()
    case "Firefox"
        driver = WebDriver.Firefox()
    case "ChromiumEdge"
        driver = WebDriver.Edge()
    default
        exit
selend


driver.Navigate("http://example.selenium.jp/reserveApp/")

// 予約フォーム画面

// 宿泊日
gettime(1)
with driver.FindElement("#reserve_year")
    .Clear()
    .SetValue(G_TIME_YY4)
endwith
with driver.FindElement("#reserve_month")
    .Clear()
    .SetValue(""+G_TIME_MM)
endwith
with driver.FindElement("#reserve_day")
    .Clear()
    .SetValue(""+G_TIME_DD)
endwith
with driver.FindElement("#reserve_term")
    .Clear()
    .SetValue("6")
endwith

// 人数
with driver.FindElement("#headcount")
    .Clear()
    .SetValue("3")
endwith

// 朝食バイキング
with driver.FindElement("#breakfast_off")
    if ! .IsSelected() then
        .Click()
    endif
endwith

// プラン
with driver.FindElement("#plan_b")
    if ! .IsSelected() then
        .Click()
    endif
endwith

// お名前
with driver.FindElement("#guestname")
    .Clear()
    .SetValue("やまだたろう")
endwith
// スクショ保存
driver.SaveScreenshot("form1.png")

// 次へ
driver.FindElement("#goto_next").Click()

// 予約内容画面
dim price
elem = driver.FindElement("#price")
if elem <> null then
    price = elem.GetText()
endif
// スクショ保存
driver.SaveScreenshot("form2.png")

// 確定
driver.FindElement("#commit").Click()

if length(price) then
    msgbox("合計で" + price + "円でした<#CR>OKで終了")
else
    msgbox("なんかうまく行ってないです")
endif
driver.Close()
